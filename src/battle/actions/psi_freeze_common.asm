
PSI_FREEZE_COMMON: ;$C295CF
    @TMPDAMAGE = $12
    @DAMAGEDONE = $14
	REP #PROC_FLAGS::ACCUM8 | PROC_FLAGS::INDEX8 | PROC_FLAGS::CARRY
	RESERVE_STACK_SPACE 22
	TAX
	STX @DAMAGEDONE
	JSR FAIL_ATTACK_ON_NPCS
	CMP #$0000
	BNE PSI_FREEZE_COMMON_RETURN
	JSR PSI_SHIELD_NULLIFY
	CMP #$0000
	BNE PSI_FREEZE_COMMON_RETURN
	LDX @DAMAGEDONE
	TXA
	JSR TWENTY_FIVE_PERCENT_VARIANCE
	STA @TMPDAMAGE
	LDX CURRENT_TARGET
	LDA a:battler::freeze_resist,X
	AND #$00FF
	TAX
	LDA @TMPDAMAGE
	JSR CALC_RESIST_DAMAGE
	TAX
	STX @DAMAGEDONE
	LDX CURRENT_TARGET
	LDA a:battler::afflictions + STATUS_GROUP::PERSISTENT_EASYHEAL,X
	AND #$00FF
	CMP #$0001
	BEQ PSI_FREEZE_COMMON_WEAKEN_SHIELD
	LDX @DAMAGEDONE
	BEQ PSI_FREEZE_COMMON_WEAKEN_SHIELD
	LDA #$0064
	JSR RAND_LIMIT
	CMP #$0019
	BCS PSI_FREEZE_COMMON_WEAKEN_SHIELD
	LDY #STATUS_2::SOLIDIFIED
	LDX #STATUS_GROUP::TEMPORARY
	LDA CURRENT_TARGET
	JSR INFLICT_STATUS_BATTLE
	CMP #$0000
	BEQ PSI_FREEZE_COMMON_WEAKEN_SHIELD
	LOADPTR MSG_BTL_KOORI_ON, $0E
	JSL DISPLAY_IN_BATTLE_TEXT
PSI_FREEZE_COMMON_WEAKEN_SHIELD:
	JSR WEAKEN_SHIELD
PSI_FREEZE_COMMON_RETURN:
	PLD
	RTS
